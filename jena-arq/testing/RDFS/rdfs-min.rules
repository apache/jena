# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# RDFS with no axioms
# Subclass, subproperty range and domain only.
# Forward rules.

[rdfs2:  (?x ?p ?y), (?p rdfs:domain ?c) -> (?x rdf:type ?c)]

[rdfs3:  (?x ?p ?y), (?p rdfs:range ?c) -> (?y rdf:type ?c)]

[rdfs5a: (?a rdfs:subPropertyOf ?b), (?b rdfs:subPropertyOf ?c) -> (?a rdfs:subPropertyOf ?c)]

## [rdfs5b: (?a rdf:type rdf:Property) -> (?a rdfs:subPropertyOf ?a)]

[rdfs6:  (?a ?p ?b), (?p rdfs:subPropertyOf ?q) -> (?a ?q ?b)]

# No X subClassOf when rdf:type used.
## [rdfs7:  (?a rdf:type rdfs:Class) -> (?a rdfs:subClassOf ?a)]

[rdfs8:  (?a rdfs:subClassOf ?b), (?b rdfs:subClassOf ?c) -> (?a rdfs:subClassOf ?c)]

[rdfs9:  (?x rdfs:subClassOf ?y), (?a rdf:type ?x) -> (?a rdf:type ?y)]

# Ways classes can be mentioned.
# rdf:type      rdfs:range  rdfs:Class
# rdfs:domain   rdfs:range  rdfs:Class
# rdfs:range    rdfs:range  rdfs:Class
# These replace (rdfs:range rdfs:range rdfs:Class) which combines with 
# rdfs7: (?a rdf:type rdfs:Class) -> (?a rdfs:subClassOf ?a)

## Needed for "combined"

[rdfsX1: (?a rdf:type ?T ) -> (?T rdfs:subClassOf ?T ) ]
[rdfsX2: (?a rdf:domain ?T ) -> (?T rdfs:subClassOf ?T ) ]
[rdfsX3: (?a rdf:range ?T ) -> (?T rdfs:subClassOf ?T ) ]
 
## X rdfs:subClassOf X only if there is a rdfs:subClassOf mentioning X.
[rdfsX4: (?a rdfs:subClassOf ?b ) , notEqual(?a,?b) -> (?a rdfs:subClassOf ?a ) ]
[rdfsX5: (?a rdfs:subClassOf ?b ) , notEqual(?a,?b) -> (?b rdfs:subClassOf ?b ) ]

## P rdfs:subClassOf P only if there is a rdfs:subPropertyOf mentioning P.
[rdfsX6: (?a rdfs:subPropertyOf ?b ) , notEqual(?a,?b) -> (?a rdfs:subPropertyOf ?a ) ]
[rdfsX7: (?a rdfs:subPropertyOf ?b ) , notEqual(?a,?b) -> (?b rdfs:subPropertyOf ?b ) ]
